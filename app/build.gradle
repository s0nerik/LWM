buildscript {
    repositories {
        maven { url 'http://download.crashlytics.com/maven' }
    }

    dependencies {
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'crashlytics'

repositories {
    flatDir {
        dirs '../libs'
    }
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url 'http://download.crashlytics.com/maven' }
}

android {
    compileSdkVersion COMPILE_SDK
    buildToolsVersion BUILD_TOOLS
    defaultConfig {
        minSdkVersion MIN_SDK
        targetSdkVersion TARGET_SDK
        versionCode 4
        versionName '0.7'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }

        release {

        }
    }
    productFlavors {
        withCrashlytics {
            buildConfigField "boolean", "CRASHLYTICS", "true"
        }
        noCrashlytics {
            buildConfigField "boolean", "CRASHLYTICS", "false"
        }
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(include: '*.jar', dir: '../libs')

    // Support libs
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.android.support:support-v4:21.0.3'
    compile 'com.android.support:recyclerview-v7:21.0.3'
    compile 'com.android.support:cardview-v7:21.0.3'
    compile 'com.android.support:palette-v7:21.0.3'

    // Compatibility
    compile 'uk.me.lewisdeane.ldialogs:ldialogs@aar'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'com.github.traex.rippleeffect:library:1.2.3'

    // UI elements
    compile('de.keyboardsurfer.android.widget:crouton:1.8.5@aar') {
        exclude group: 'com.google.android', module: 'support-v4'
    }
    compile('com.jpardogo.materialtabstrip:library:1.0.6') {
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'appcompat-v7'
    }
    compile 'com.github.nirhart:parallaxscroll:1.0'
    compile 'com.melnykov:floatingactionbutton:1.1.0'

    // Serialization
    compile 'com.google.code.gson:gson:2.3.1'

    // Networking
    compile 'com.koushikdutta.ion:ion:1.4.1'
    compile 'org.java-websocket:Java-WebSocket:1.3.0'

    // Crash reports
    compile 'com.crashlytics.android:crashlytics:1.1.13'

    // Thanks, Square!
    compile 'com.squareup.dagger:dagger:1.2.2'
    compile 'com.squareup.dagger:dagger-compiler:1.2.2'
    compile 'com.squareup:otto:1.3.5'
    compile 'com.jakewharton:butterknife:6.0.0'

    // F*CK YOU, BOILERPLATE! =)
    provided 'org.projectlombok:lombok:1.14.8'

    // Library projects
    compile project(':libs:android-stackblur')
    compile project(':libs:Fontify')
    compile project(':libs:ExoPlayer')

}