apply plugin: "com.android.application"
apply plugin: 'groovyx.grooid.groovy-android'
apply plugin: 'net.kaleidos.gradle.plugins.emerger'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'io.fabric'
apply plugin: 'com.fernandocejas.frodo'

repositories {
    flatDir {
        dirs '../libs'
    }
    jcenter()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'http://dl.bintray.com/amulyakhare/maven' }
    maven { url 'http://raw.github.com/wada811/Android-Material-Design-Colors/master/repository/' }
    maven { url "http://dl.bintray.com/s0nerik/maven" }
    maven { url "http://dl.bintray.com/tbruyelle/tbruyelle" }
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion "android-23"
    buildToolsVersion '23.0.3'
    defaultConfig {
        applicationId "com.lwm.app"
        minSdkVersion 21
        targetSdkVersion 22
        versionCode 4
        versionName '0.7'
        renderscriptTargetApi 23
        renderscriptSupportModeEnabled true
        multiDexEnabled true
    }
    dexOptions {
        incremental true
        preDexLibraries = true
        javaMaxHeapSize "4g"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }

        release {}
    }
    flavorDimensions "type", "crashlytics"
    productFlavors {
        dev {
            dimension "type"
        }
        prod {
            dimension "type"
        }

        withCrashlytics {
            dimension "crashlytics"
            buildConfigField "boolean", "CRASHLYTICS", "true"
        }
        noCrashlytics {
            dimension "crashlytics"
            buildConfigField "boolean", "CRASHLYTICS", "false"
        }
    }
    packagingOptions {
        exclude 'META-INF/services/org.codehaus.groovy.transform.ASTTransformation'
        exclude 'META-INF/services/org.codehaus.groovy.runtime.ExtensionModule'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/groovy-release-info.properties'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/DISCLAIMER'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
    lintOptions {
        disable 'InvalidPackage'
        abortOnError false
    }

    useLibrary 'org.apache.http.legacy'
}

androidGroovy {
    skipJavaC = true
}

dependencies {
    compile fileTree(include: '*.jar', dir: '../libs')

    compile 'org.codehaus.groovy:groovy:2.4.7:grooid'
    compile('org.codehaus.groovy:groovy-json:2.4.7') {
        transitive = false
    }

    // Support libs
    compile 'com.android.support:appcompat-v7:24.0.0'
    compile 'com.android.support:support-v4:24.0.0'
    compile 'com.android.support:recyclerview-v7:24.0.0'
    compile 'com.android.support:cardview-v7:24.0.0'
    compile 'com.android.support:palette-v7:24.0.0'
    compile 'com.android.support:design:24.0.0'

    // GMS
    compile 'com.google.android.gms:play-services-plus:8.3.0'
    compile 'com.google.android.gms:play-services-identity:8.3.0'

    // UI elements
    compile 'de.hdodenhof:circleimageview:1.2.1'
    compile 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.0'
    compile 'com.wada811:android-material-design-colors:2.0.0'
    compile 'com.github.dmytrodanylyk.shadow-layout:library:1.0.3'
    compile 'com.github.ksoichiro:android-observablescrollview:1.6.0'
    compile 'eu.davidea:flexible-adapter:5.0.0-b4'
    compile 'com.github.renaudcerrato:ToggleDrawable:1.0.2'

    // Image loading
    compile 'com.github.bumptech.glide:glide:3.6.1'
    compile 'jp.wasabeef:glide-transformations:2.0.1'
    compile 'com.github.florent37:glidepalette:1.0.2@aar'

    // Networking
    compile 'com.nanohttpd:nanohttpd:2.1.1'
    compile 'org.java-websocket:Java-WebSocket:1.3.0'
    compile 'com.koushikdutta.ion:ion:+'

    // Crashlytics kit
    compile('com.crashlytics.sdk.android:crashlytics:2.5.3@aar') {
        transitive = true
    }

    // Thanks, Square!
    compile 'com.squareup.dagger:dagger:1.2.2'
    apt 'com.squareup.dagger:dagger-compiler:1.2.2'
    compile 'com.squareup:otto:1.3.8'

    // Thanks, Apache!
    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'org.apache.commons:commons-collections4:4.0'

    // Misc
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.github.talenguyen:prettysharedpreferences:1.0.2'
    compile 'ru.noties:debug:1.1.3'

    // Rx
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.6'
    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.2.0@aar'
    compile 'com.jakewharton.rxbinding:rxbinding-recyclerview-v7:0.3.0'
    compile 'com.jakewharton.rxbinding:rxbinding-design:0.3.0'

    compile('com.github.s0nerik.betterknife:BetterKnife:1.3.13@aar') {
        transitive = false
    }

    compile 'com.google.android.exoplayer:exoplayer:r1.5.5'
}